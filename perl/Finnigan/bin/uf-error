#!/usr/bin/env perl

use strict;
use warnings FATAL => qw( all );
use feature qw/state say/;
use 5.010;

use Finnigan;

@ARGV == 1 or do {
  say STDERR "Usage: $0 <file>";
  exit -1;
};
my $file = $ARGV[0];
-e $file or do {
  say STDERR "file '$file' does not exist";
  exit -1;
};
-f $file or do {
  say STDERR "'$file' is not a plain file";
  exit -1;
};
-s $file or do {
  say STDERR "'$file' has zero size";
  exit -1;
};

# -----------------------------------------------------------------------------
open INPUT, "<$file" or die "can't open '$file': $!";
binmode INPUT;

my $header = Finnigan::FileHeader->decode(\*INPUT);
my $VERSION = $header->version;
my $seq_row = Finnigan::SeqRow->decode(\*INPUT, $VERSION);
my $cas_info = Finnigan::ASInfo->decode(\*INPUT);
my $rfi = Finnigan::RawFileInfo->decode(\*INPUT, $VERSION);


my $run_header_0_addr = $rfi->preamble->run_header_addr(0);
my $run_header_1_addr;
if ( $rfi->preamble->run_header_addr(0) ) {
  # There is more than one data stream; see which of the run headers to use
  $run_header_1_addr = $rfi->preamble->run_header_addr(1);
}

# fast-forward to RunHeader
seek INPUT, $run_header_0_addr, 0;
my $run_header_0 = Finnigan::RunHeader->decode(\*INPUT, $VERSION);
my $run_header = $run_header_0;

my $run_header_1;
if ($run_header_1_addr) {
  seek INPUT, $run_header_1_addr, 0;
  $run_header_1 = Finnigan::RunHeader->decode(\*INPUT, $VERSION);
}

# and now skip to the error log
my $first_log_exists;
my $error_log_addr = $run_header_0->error_log_addr;
seek INPUT, $error_log_addr, 0;

# In all known samples before v.66, the error log length was written here (as well as in SampleInfo)
my $error_log_length = Finnigan::Decoder->read(\*INPUT, ['length' => ['V', 'UInt32']])->{data}->{length}->{value};

# In v.66, it is always set to 0, even when there are error messages. The value in SampleInfo seems to be more reliable.
$error_log_length = $run_header_0->sample_info->error_log_length unless $error_log_length;

if ( $error_log_length ) {
  if ( $run_header_1_addr ) {
   $first_log_exists++;
    say "------------------------ Error log 1 ---------------------------";
  }
  foreach my $i ( 0 .. $error_log_length - 1) {
    my $e = Finnigan::Error->decode(\*INPUT);
    say STDERR $e->time . "\t" . $e->message;
  }
}

# try the second error log if it exists
if ( $run_header_1_addr ) {
  $error_log_addr = $run_header_1->error_log_addr;
  seek INPUT, $error_log_addr, 0;

  my $error_log_length = Finnigan::Decoder->read(\*INPUT, ['length' => ['V', 'UInt32']])->{data}->{length}->{value};

  # In v.66, it is always set to 0, even when there are error messages. The value in SampleInfo seems to be more reliable.
  $error_log_length = $run_header_1->sample_info->error_log_length unless $error_log_length;

  if ( $error_log_length ) {
    if ( $first_log_exists ) {
      say "------------------------ Error log 2 ---------------------------";
    }
    foreach my $i ( 0 .. $error_log_length - 1) {
      my $e = Finnigan::Error->decode(\*INPUT);
      say STDERR $e->time . "\t" . $e->message;
    }
  }
}
__END__
=head1 NAME

uf-error - print the error log entries from a Finnigan raw file

=head1 SYNOPSIS

uf-log <file>

=head1 DESCRIPTION

B<uf-error> prints the list of messages from the embedded error log in
a Finnigan raw file. The messages are timestamped with retention time.

It will exit with no output if there are no error messages.

=head1 SEE ALSO

Finnigan::Error

=cut
